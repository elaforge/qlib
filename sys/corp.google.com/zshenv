# Turn off google setup crap.
google_zsh_flysolo=true

path=(
    # java/jdk/bin expects to override /usr/bin/java
    /usr/local/buildtools/java/jdk/bin
    $path
    /usr/local/scripts /usr/local/symlinks
)
path=($path ~/.cabal/bin)

path=(~/bin/$host ~/bin ~/bin/$(~/bin/oname) /usr/local/bin $path)

unset GREP_OPTIONS GREP_COLOR # kill google default env

pythonpath=($HOME/lib/python /home/build)

if [[ -n "$DISPLAY" ]]; then
  export G4MULTIDIFF=1
fi

: ${P4CONFIG:=.p4config}
# : ${P4DIFF:=/home/build/public/google/tools/p4diff -w}
: ${P4DIFF:=meldorgit.sh}
: ${P4MERGE:=/home/build/public/eng/perforce/mergep4.tcl}
: ${P4EDITOR:=$EDITOR}
export P4CONFIG P4DIFF P4MERGE P4EDITOR

# In zshenv so scripts get them.

fileutil_args=(
  --gfs_user=gt-team --sharded
  --parallel_copy=40 --parallel_rm=40 --parallel_chgrp=40 --parallel_chown=40
  --colossus_parallel_copy
)

function f {
  fileutil $fileutil_args "$@"
}

# build with special flags for flume
function fbuild {
    blaze build --nolegacy_java_swigdeps $1_deploy.jar
}

function bpath {
    local cmd=${1%.py}
    local cwd=$(builtin pwd -r)/$cmd
    echo ${cwd/\/google3\//\/google3\/blaze-bin\/}
}

# Convert com.google.whatever into google3 path.
function jpath {
    local sym=$1
    if [[ $1 != com.google.* ]]; then
        sym=com.google.$sym
    fi
    echo /home/build/google3/java/${sym//./\/}
}

function brun {
    local cmd=$(bpath $1)
    shift
    $cmd "$@"
}

function jrun {
  local bin=$1
  shift
  brun $bin --jvm_flags='-Djava.util.logging.config.file=/home/elaforge/java-log-config' "$@"
}

function jtest {
  # blaze test SomeTest --test_arg=com.google.geo.gt.toolbox.GenerateIssueDataForDremelTest#method
  # jtest SomeTest TestClass.java testMethod
}

function g3 {
  local pwd=$(command pwd)
  if [[ $pwd = */google3* ]]; then
    echo ${pwd/google3*/}google3/
  else
    echo no google3 in $pwd >&2
    return 1
  fi
}

# magicjar

function mjrun {
  local cmd=$1
  shift
  if [[ -n $suspend ]]; then
    export DEFAULT_JVM_DEBUG_SUSPEND=y
  else
    export DEFAULT_JVM_DEBUG_SUSPEND=n
  fi
  $(bpath $cmd) --debug \
    --main_advice_classpath=$(magicjar --google3 $(g3) -pwd)/eclipse/bin \
    "$@"
}

# magicbuild wants to be run from google3
function magicbuild {
  (cd $(g3) && command magicbuild "$@")
}
