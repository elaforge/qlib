# kill annoying default aliases and functions
# *why* must people fiddle with the default setup!?
unhash -am '*'
unhash -fm '*'

# New linux likes to put annoying junk in system profiles.
# /etc/zshenv will still run though.
setopt no_global_rcs
# Disable whatever functions happen to be lurking.
fpath=()

# On some systems, some of the time,  this says zle-not-something and messes up
# the terminal.
# bindkey -d

# not sure where or how this gets set, but it messes up automatic path stuff
unset MANPATH
skip_global_compinit=1

host=$(hostname -s)
if which dnsdomainname >/dev/null; then
    domain=$(dnsdomainname)
else
    domain=nodomain
fi
if [[ $domain = c.groq*.internal ]]; then
    domain=groq
fi

export host domain
oname=$($qlib/bin/oname)

# typeset -T PATH path # automatic
# establish a:b:c <-> (a b c) link
typeset -T PYTHONPATH pythonpath
export PYTHONPATH
typeset -U path pythonpath
export PYTHONSTARTUP=$qlib/dotfiles/pythonstartup.py

extra_path=(
    ~/bin ~/.cabal/bin ~/.cargo/bin
    /usr/local/bin
    $qlib/sys/$host/bin $qlib/sys/$oname/bin $qlib/sys/$domain/bin
)
for dir in $extra_path; do
    # Don't mess with the path order if they're already in there.
    # Otherwise, I wind up putting these back in front of a nix-shell PATH.
    if [[ -d $dir && ${path[(ie)$dir]} -gt ${#path} ]]; then
        path=($dir $path)
    fi
done
unset extra_path

# On OS X, path_helper in /etc/zprofile will rearrange the path.  I just
# commented it out.

pythonpath=(~/lib/python)

function _trydot {
    if [[ -r $1 ]]; then
        . $1
    fi
}

export PAGER=less
export EDITOR=vim
export LESS='--RAW-CONTROL-CHARS --LONG-PROMPT --quiet'
    # Try this with git, so I can get colors and no less.  --no-init disables
    # page flipping, though I sometimes like it.
    # --quit-if-one-screen --no-init
# For whatever reason, systemctl decides to ignore $LESS in favor of its own
# annoying defaults.
export SYSTEMD_LESS=$LESS

export LANG=en_US.utf-8
# Otherwise sort sorts in a surprising order.  I can't use LC_ALL because that
# will change text input, and then haskell gets upset at non-ASCII.
export LC_COLLATE=C

_trydot /nix/var/nix/profiles/default/etc/profile.d/nix-daemon.sh
_trydot ~/.nix-profile/etc/profile.d/nix-daemon.sh

_trydot $qlib/sys/$host/zshenv
_trydot $qlib/sys/$domain/zshenv
