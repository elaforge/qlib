# kill annoying default aliases and functions
# *why* must people fiddle with the default setup!?
unhash -am '*'
unhash -fm '*'

# On some systems, some of the time,  this says zle-not-something and messes up
# the terminal.
# bindkey -d

# not sure where or how this gets set, but it messes up automatic path stuff
unset MANPATH
skip_global_compinit=1

host=$(/bin/hostname -s)
if which dnsdomainname >/dev/null; then
    domain=$(dnsdomainname)
else
    domain=nodomain
fi
if [[ $domain = c.groq*.internal ]]; then
    domain=groq
fi

export host domain
oname=$($qlib/bin/oname)

# typeset -T PATH path # automatic
# establish a:b:c <-> (a b c) link
typeset -T PYTHONPATH pythonpath
export PYTHONPATH
typeset -U path pythonpath
export PYTHONSTARTUP=$qlib/dotfiles/pythonstartup.py

path=(
    ~/bin ~/.cabal/bin ~/.cargo/bin
    $qlib/sys/$host/bin $qlib/sys/$oname/bin $qlib/sys/$domain/bin
    $path
)
# On OS X, path_helper in /etc/zprofile will rearrange the path, but
# I shouldn't be overriding system binaries anyway.

pythonpath=(~/lib/python)

function _trydot {
    if [[ -r $1 ]]; then
        . $1
    fi
}

export PAGER=less
export EDITOR=vim
export LESS='--RAW-CONTROL-CHARS --LONG-PROMPT --quiet'
    # Try this with git, so I can get colors and no less.  --no-init disables
    # page flipping, though I sometimes like it.
    # --quit-if-one-screen --no-init

export LANG=en_US.UTF-8
# Otherwise sort sorts in a surprising order.  I can't use LC_ALL because that
# will change text input, and then haskell gets upset at non-ASCII.
export LC_COLLATE=C

_trydot /Users/elaforge/.nix-profile/etc/profile.d/nix.sh

_trydot $qlib/sys/$host/zshenv
_trydot $qlib/sys/$domain/zshenv
